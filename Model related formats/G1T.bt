//------------------------------------------------
//--- 010 Editor v9.0.2 Binary Template
//
//      File: Koei Tecmo Texture Container
//   Authors: Raytwo
//   Version: 1.2
//   Purpose: Parsing G1T texture containers
//  Category: File Format
// File Mask: *.g1t
//  ID Bytes: /
//   History:
//   1.0    2019-08-22  Raytwo: Made the original file
//   1.1    2019-08-23  Raytwo: Added the normal map flag table
//   1.1    2019-09-08  Raytwo: Added support for RGBA8 internal format
//   1.2    2019-11-28  HealingBrew: Updated MipCount, Subsystem, and Dimension logic.
//------------------------------------------------

LittleEndian();

enum<byte> DXT_TYPE
{
    BGRA8 = 0x1,
    RGBA8 = 0x2,
    DXT1 = 0x59,
    DXT5 = 0x5B
};

enum<byte> G1TG_SUBSYSTEM
{
    Generic,
    PS4, // Treated the same as Xbox
    PSVita,
    PS3,
    Xbox
};

enum<short> G1TG_FLAGS 
{
    G1TG_FLAGS_MODERN = 0x1,
    G1TG_FLAGS_HAS_EXTRA = 0x1000,
    G1TG_FLAGS_EXTRA = G1TG_FLAGS_HAS_EXTRA | G1TG_FLAGS_MODERN
};

struct FILE {
    if(ReadUInt() == 0x47543147) {
        BigEndian();
    }

    struct HEADER {
        char magic[4]<name="Magic", fgcolor=cLtBlue>;
        char version[4]<name="Version", fgcolor=cLtBlue>;
        uint32 filesize<name="Filesize", fgcolor=cLtYellow, format=hex>;
        uint32 tableOffset<name="Offset table address", fgcolor=cLtRed,format=hex>;
        uint32 entryCount<name="Number of textures", fgcolor=cLtGreen, format=decimal>;
        uint32 unk;
        uint32 unk;
    }Header<name="Header">;
    
    uint32 NormalMapTable[Header.entryCount]<name="Normal map flag?", comment="Entries with 3 are normal maps", fgcolor=cAqua>;
    uint32 OffsetTable[Header.entryCount]<name="Offset table", comment="Contains pointers to all the textures relative to the beginning of the table", fgcolor=cLtRed>;

    local int i = 0;

    for(i = 0;i < Header.entryCount;i++)
    {
        FSeek(Header.tableOffset + OffsetTable[i]);

        struct TEXHEADER {
            byte subsystem_id : 4<name="Subsystem">,
                 mip_count : 4<name="Mip Count", comment="SHR 4">;
            local G1TG_SUBSYSTEM subsystem = (G1TG_SUBSYSTEM) subsystem_id;
            DXT_TYPE type<name="DXT Type", format=hex>;
            byte packed_width : 4<name="Width">,
                 packed_height : 4<name = "Height", comment="SHR 4">;
            byte unknown1<name="Unknown 1">;
            
            byte unknown2<name="Unknown 2">;
            byte unknown3<name="Unknown 3">;
            G1TG_FLAGS flags<name="Flags", comment="If the last byte is 0x10, the extra header is used", format=hex, fgcolor=cLtYellow>;
            Printf("%d\n", (flags & G1TG_FLAGS_EXTRA));
            if((flags & G1TG_FLAGS_EXTRA) == G1TG_FLAGS_EXTRA)
            {
                struct EXTRATEXHEADER {
                    uint32 size<name="Extra Texture Header size", format=hex>;
                    float unknown1;
                    uint16 unknown2;
                    uint16 unknown3;
                    if(size >= 0x10) {
                        int width;
                    }
                    if(size >= 0x14) {
                        int height;
                    }
                }ExtraTexHeader<name="Extra Texture Header", comment="Width and Height values for non-powers of two and large textures", bgcolor=cDkGray>;
            }
            
            local uint32 Width = packed_width > 0 ? 1 << packed_width : ExtraTexHeader.width;
            local uint32 Height = packed_height > 0 ? 1 << packed_height : ExtraTexHeader.height;

            local uint64 Size = 0;

            if(type == 0x1 || type == 0x2) //RGBA8 and BGRA8
            {
                Size = (Width * Height) * 4;
            }
            if(type == 0x59) //DXT1
            {
                Size = (Width * Height) / 2;
            }
            if(type == 0x5B) //DXT5
            {
                Size = Width * Height;
            }
            
            // Read mipmaps
            local int j = 0;
            local int originalSize = Size;

            for(j = 1;j < mip_count; j++) //Skip the first entry which is the full-sized texture
            {
                originalSize = originalSize/4;
                Size += originalSize;
            }

            ubyte Textures[Size]<name="Textures data", comment="Textures are stored with their mipmaps in this array", format=hex, open=suppress, bgcolor=cDkBlue, fgcolor=cLtAqua>;
        }TexHeader<name="Texture", fgcolor=cLtGreen>;
    }
}File<name="G1T file", open=true>;